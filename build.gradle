plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
    id 'org.jetbrains.kotlin.plugin.spring' version "${kotlinVersion}"
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'idea'
}

group 'com.damianryan.spikes'
version '1.0-SNAPSHOT'

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = [ "-Xjsr305=strict" ] // enforce null safety for annotated Spring types
        jvmTarget = "13"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: springBootVersion)

    compileOnly group: 'org.springframework.boot', name: 'spring-boot-devtools' // having this stops error logs appearing

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin'

    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: logstashLogbackEncoderVersion
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib'
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
}

bootJar {
    archiveFileName = "${project.name}.jar"
    doFirst {
        manifest {
            attributes 'Implementation-Version': project.version
        }
    }
}

bootRun.doFirst {
    systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active", "logstash")
}

processResources {
    filesMatching('logback-spring.xml') {
        expand(project.properties)
    }
}
